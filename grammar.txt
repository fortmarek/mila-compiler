Program -> Dekl SložPříkaz
Dekl -> DeklKonst Dekl 
Dekl -> DeklProm Dekl 
Dekl -> ε
DeklKonst -> const ident = číslo ZbDeklKonst ;
ZbDeklKonst -> , ident = číslo ZbDeklKonst 
ZbDeklKonst -> ε
DeklProm -> var ident ZbDeklProm ;
ZbDeklProm -> , ident ZbDeklProm
ZbDeklProm -> ε 
 SložPříkaz -> begin Instruction ZbPříkazů end
 ZbPříkazů -> ; Instruction ZbPříkazů
ZbPříkazů->ε
Instruction -> ident := Výraz
Instruction -> write Výraz
Instruction -> IfBlock
Instruction -> while Condition do Instruction 
Instruction -> SložPříkaz
Instruction ->ε
IfBlock -> if Condition then Instruction ElseBlock
ElseBlock -> else Instruction 
ElseBlock -> ε 
Condition -> Výraz RelOp Výraz
RelOp -> =
RelOp -> <>
RelOp -> <
RelOp -> >
RelOp -> <=
RelOp -> >=
Výraz -> Term ZbVýrazu
Výraz -> - Term ZbVýrazu
ZbVýrazu -> + Term ZbVýrazu
ZbVýrazu -> - Term ZbVýrazu
ZbVýrazu ->ε
Term -> Faktor ZbTermu
ZbTermu -> * Faktor ZbTermu
ZbTermu -> / Faktor ZbTermu
ZbTermu -> ε
Faktor -> ident
Faktor -> číslo 
Faktor  -> ( Výraz ) 