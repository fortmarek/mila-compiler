Program -> Dekl SložPříkaz
Dekl -> DeklKonst Dekl 
Dekl -> DeklProm Dekl 
Dekl -> ε
DeklKonst -> const ident = number ZbDeklKonst ;
ZbDeklKonst -> , ident = number ZbDeklKonst 
ZbDeklKonst -> ε
DeklProm -> var ident ZbDeklProm ;
ZbDeklProm -> , ident ZbDeklProm
ZbDeklProm -> ε 
SložPříkaz -> begin Instruction ZbPříkazů end
ZbPříkazů -> ; Instruction ZbPříkazů
ZbPříkazů -> ε
Instruction -> ForBlock
Instruction -> ident := Expression
Instruction -> write Expression
Instruction -> IfBlock
Instruction -> WhileBlock 
Instruction -> SložPříkaz
Instruction ->ε
WhileBlock -> while Condition do Instruction
ForBlock -> for ident := Expression do begin Instruction end;
IfBlock -> if Condition then Instruction ElseBlock
ElseBlock -> else Instruction 
ElseBlock -> ε 
Condition -> Expression RelOp Expression
RelOp -> =
RelOp -> <>
RelOp -> <
RelOp -> >
RelOp -> <=
RelOp -> >=
Expression -> Term ZbVýrazu
Expression -> - Term ZbVýrazu
ZbVýrazu -> + Term ZbVýrazu
ZbVýrazu -> - Term ZbVýrazu
ZbVýrazu ->ε
Term -> Faktor ZbTermu
ZbTermu -> * Faktor ZbTermu
ZbTermu -> / Faktor ZbTermu
ZbTermu -> ε
Faktor -> ident
Faktor -> number 
Faktor  -> ( Expression ) 